// SPDX-FileCopyrightText: 2024 The EDDIE Developers <eddie.developers@fh-hagenberg.at>
// SPDX-License-Identifier: Apache-2.0

import net.ltgt.gradle.errorprone.CheckSeverity
import net.ltgt.gradle.errorprone.errorprone
import org.springframework.boot.gradle.tasks.bundling.BootJar
import java.util.*

plugins {
    id("energy.eddie.java-conventions")
    id("energy.eddie.pnpm-build")

    alias(libs.plugins.openapi.generator)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

group = "energy.eddie"
version = "0.0.0"

repositories {
    mavenCentral()
}

// JAXB configuration holds classpath for running the JAXB XJC compiler
val jaxb: Configuration by configurations.creating
dependencies {
    implementation(project(":api"))
    implementation(project(":data-needs"))
    implementation(project(":region-connectors:shared"))
    implementation(libs.spring.boot.starter.web)
    implementation(libs.spring.boot.starter.thymeleaf)
    implementation(libs.spring.boot.starter.data.jpa)
    implementation(libs.spring.boot.starter.validation)
    implementation(libs.spring.boot.starter.webflux)
    implementation(libs.spring.boot.starter.webclient)
    implementation(libs.spring.oxm)
    implementation(libs.spring.boot.starter.actuator)
    implementation(libs.spring.boot.security)

    // for atom feed
    implementation(libs.rometools.rome)

    // dependencies needed to generate code
    jaxb(libs.jaxb.xjc)
    jaxb(libs.jaxb.runtime)
    jaxb(libs.jaxb.plugins)

    testImplementation(libs.spring.boot.starter.test)
    testImplementation(libs.spring.boot.starter.webmvc.test)
    testImplementation(libs.spring.boot.starter.data.jpa.test)
    testImplementation(libs.reactor.test)
    testImplementation(libs.testcontainers.postgresql)
    testImplementation(libs.testcontainers.junit)
    testImplementation(libs.spring.boot.testcontainers)
    testImplementation(libs.okhttp3.mockwebserver)

    testRuntimeOnly(libs.postgresql)
    testRuntimeOnly(libs.spring.boot.starter.flyway)
    testRuntimeOnly(libs.flyway.postgresql)
}

// Directory for generated java files
val generatedXJCJavaDir = "${project.layout.buildDirectory.asFile.get().absolutePath}/generated/sources/xjc/main/java"

// Add generated sources to the main source set
sourceSets {
    main {
        java {
            srcDir(generatedXJCJavaDir)
        }
    }
    test {
        java {
            srcDir(generatedXJCJavaDir)
        }
    }
}


val generateGreenButtonClassesName = "generateGreenButtonClasses"
tasks.register(generateGreenButtonClassesName, JavaExec::class) {
    description = "Generate Green Button Java Classes from XSD files"
    group = JavaBasePlugin.BUILD_DEPENDENTS_TASK_NAME
    classpath(jaxb)
    mainClass.set("com.sun.tools.xjc.XJCFacade")

    // make sure the directory exists
    file(generatedXJCJavaDir).mkdirs()

    // Path to XSD files
    val greenButtonSchemaFiles = "src/main/schemas/green-button/xsd/"

    // explicitly set the encoding because of rare issues discovered on Windows 10
    args(
        "-d",
        generatedXJCJavaDir,
        greenButtonSchemaFiles,
        "-mark-generated",
        "-npa",
        "-encoding",
        "UTF-8",
        "-extension",
        "-Xfluent-api"
    )

    // Define the task inputs and outputs, so Gradle can track changes and only run the task when needed
    inputs.files(fileTree(greenButtonSchemaFiles).include("**/GB_XML_schema_usage_v3_3.xsd"))
    outputs.dir(generatedXJCJavaDir)
}


tasks.named("compileJava") {
    // generate the classes before compiling
    dependsOn(generateGreenButtonClassesName)
}

sourceSets.configureEach {
    java.srcDir(tasks.named("generateGreenButtonClasses").map { files() })
}

tasks.withType<JavaCompile>().configureEach {
    options.errorprone.disableWarningsInGeneratedCode.set(true)
    if (!name.lowercase(Locale.getDefault()).contains("test")) {
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "energy.eddie.regionconnector.us")
        }
    }
}

// exclude the following classes / packages from test coverage
val generatedSchemas = "org/naesb/espi/*" // these files are generated by the generateEDASchemaClasses task

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    finalizedBy(tasks.jacocoTestCoverageVerification)
    classDirectories.setFrom(
        sourceSets.main.get().output.asFileTree.matching {
            exclude(generatedSchemas)
        }
    )
}

tasks.test {
    useJUnitPlatform()
}

// disable bootJar task as it needs a main class and region connectors do not have one
tasks.getByName<BootJar>("bootJar") {
    enabled = false
}

tasks.getByName<Jar>("jar") {
    enabled = true
}